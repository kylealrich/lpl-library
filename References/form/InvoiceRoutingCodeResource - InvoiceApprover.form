InvoiceApprover is a Form
    is primary
    title is "InvoiceApprovalLevel"
    Layout
        paragraph
            VendorGroup
            VendorGroup.Description
                no label
                display as text
        paragraph
            InvoiceRoutingCode
            InvoiceRoutingCode.Description
                no label
                display as text
        single column
            visible when (ApprovalLevel entered)
                ApprovalLevel
                    display as text
        visible when (not InvoiceRoutingCode.AssignApproversUsingAuthorityCodes)
            visible when (not InvoiceRoutingCode.UsesTeamMemberAssignment)
                paragraph
                    Approver
                    Approver.PreferredSimplePresentationName
                        no label
                        display as text
                single column
                    text of "-_Or_-"
            paragraph
                ApprovalTeam
                    select is PayablesFinanceTeamSelect
                ApprovalTeam.Description
                    no label
                    display as text
        visible when (InvoiceRoutingCode.AssignApproversUsingAuthorityCodes)
            paragraph
                InvoiceApprovalAuthCode
                    select is InvoiceApprovalRolesSelect
                InvoiceApprovalAuthCode.Description
                    no label
                    display as text
        single column
            blank line
            paragraph
                MaxApprovalAmount
                InvoiceRoutingCode.Currency
                    no label
                    display as text
                visible when (InvoiceRoutingCode.UsesTeamMemberAssignment)
                    text of "-_Or_-"
                    AmountDefinedAtMemberLevel
                        raise red alert when (RoutingTeamMemberMissingMaxAmount)
                            mouse over text is "TeamMembersMissingMaxApprovalAmount"
        paragraph
            visible when (not InvoiceRoutingCode.UsesTeamMemberAssignment)
                OverrideNextApprovalLevel
                    label is "SkipNextApprovalLevel"
                        left of field
                visible when (OverrideNextApprovalLevel)
                    NextApprovalLevelAfterApprove
                        label is "SkipToLevel"
                        select is InvoiceApproversSelect
                    visible when (NextApprovalLevelAfterApproveApproverEntered)
                        NextApprovalLevelAfterApprove.Approver.PreferredSimplePresentationName
                            label is "Approver"
                            display as text
                        NextApprovalLevelAfterApprove.ApprovalLevel
                            display as text
                    visible when (NextApprovalLevelAfterApproveApprovalTeamEntered)
                        NextApprovalLevelAfterApprove.ApprovalTeam.Description
                            label is "ApprovalTeam"
                            display as text
                        NextApprovalLevelAfterApprove.ApprovalLevel
                            display as text
        header2 of "EscalationSettings"
        paragraph
            EscalationDays
                label is "EscalateAfter"
            text of "DaysTo"
            EscalateTo
                no label
        paragraph
            visible when (EscalateTo.SpecificApprovalLevel)
                NextApprovalLevelAfterEscalation
                    label is "ApprovalLevel"
                    select is InvoiceApproversSelect
                visible when (NextApprovalLevelAfterEscalationApproverEntered)
                    text of "at"
                    NextApprovalLevelAfterEscalation.ApprovalLevel
                        display as text
                    text of "with"
                    NextApprovalLevelAfterEscalation.Approver.PreferredSimplePresentationName
                        label is "ApproverOf"
                        display as text
                visible when (NextApprovalLevelAfterEscalationApprovalTeamEntered)
                    text of "at"
                    NextApprovalLevelAfterEscalation.ApprovalLevel
                        display as text
                    text of "with"
                    NextApprovalLevelAfterEscalation.ApprovalTeam.Description
                        label is "ApprovalTeamOf"
                        display as text
            visible when (EscalateToNextLevel)
                DerivedApprovalLevelPlusOne
                    label is "ApprovalLevel"
                    display as text
                text of "with"
                visible when (NextApprovalLevelApproverEntered)
                    DerivedNextApprovalLevelApprover
                        label is "ApproverOf"
                        display as text
                visible when (NextApprovalLevelApprovalTeamEntered)
                    DerivedNextApprovalLevelTeam
                        label is "ApprovalTeamOf"
                        display as text
        blank line
        paragraph
            visible when (InvalidNextLevelEscalation)
                text of "<InvalidNextLevelEscalationMessage>"
                    bold
                    color of blue
        paragraph
            visible when (InvalidSpecificLevelEscalation)
                text of "<InvalidSpecificLevelEscalationMessage>"
                    bold
                    color of red
