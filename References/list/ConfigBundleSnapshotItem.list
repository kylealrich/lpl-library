AppliedConsoleConfigurationList is a List
    title is "SnapshotItems"
    single select
    search form is AppliedConsoleSearchForm
        use as filter
    show as simple list
    Display Fields
        ConfigAppliedStatus.Status
            label is "AppliedOperation"
            display as tag
                color of yellow when (ConfigAppliedStatus.Status.NotApplied)
                color of green when (ConfigAppliedStatus.Status.Created)
                color of red when (ConfigAppliedStatus.Status.Deleted)
                color of blue when (ConfigAppliedStatus.Status.Updated)
                color of purple when (ConfigAppliedStatus.Status.NoOperation)
        ConfigSnapshotItemInfo.BusinessView
        ConfigSnapshotItemInfo.ConfigEntityType
            left align
            raise yellow alert when (DeletedRecord)
                mouse over text is "MarkedForDeletion"
        ConfigSnapshotItemInfo.Name
            bold
        ConfigSnapshotItemInfo.Actor
        ReportOrFolderMsg
            raise yellow alert when (IsReportOrFolder)
                mouse over text is "<ReportFolderTypeMessage>"
        Status
        DerTags
        ConfigValidationStatus.Status
            label is "Validation"
            raise red alert when (ValidationFailed)
                mouse over text is "<ErrorText>"
            raise yellow alert when (ConfigValidationStatus.Status.NotValidated)
                mouse over text is "<ConfigValidationStatus.Status>"
        ConfigValidationStatus.LastValidationTime
            column visible when (not ConfigBundleSnapshot.ConfigValidationStatus.Status.NotValidated)
        button of "Documentation"
            visible when (HasDocumentation)
            view Documentation.File
            label is "_"
            display as link
    open action is restricted
    Detail Sections
        SnapshotLPL
            form is SnapshotLPLForm
        Configuration_vs_Snapshot
            form is SnapshotCompareAgainstConfigurationForm
        Validation
            visible when (AnyValidationFailed)
            form is ValidationForm
