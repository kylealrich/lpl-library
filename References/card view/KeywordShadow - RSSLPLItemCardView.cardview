RSSLPLItemCardView is a CardView
    show labels
    left column is "<DerivedImageURL>"
        visible when (UseLinkedImage)
        display as full image
    left column is DerivedPicture
        visible when (not UseLinkedImage)
        display as full image
        missing image is random background
            foreground text is "<ItemAbbreviation>"
    main column
        Item.Picture.Title
            visible when (false)
        ItemDescription
            no label
            bold
        DerivedItem
            no label
            link is RSSLPLCatalogItemDetailView
        OnContractLabel
            no label
            link is ContractLineNav
            visible when (HasContractLineAndDisplayContract)
        ReplacementItemLabel
            no label
            color of green
            display as tag
            visible when (IsReplacementItem)
        KitItemLabel
            no label
            link is RSSLPLKitItemComponents
            display as tag
                color of blue
            visible when (IsKitItem)
        RecallLabel
            no label
            link is RecallNav
            display as tag
                color of red
            visible when (RecallNoticesExist)
        LatexLabel
            no label
            display as tag
                color of orange
            visible when (LatexItem)
        SubstituteItemLabel
            no label
            link is RSSLPLSubstituteItems
            display as tag
                color of gray
            visible when (HasSubstituteItem)
        SpecialHandlingCodeLabel
            no label
            display as tag
                color of yellow
            visible when (HasSpecialHandlingCode)
        CompanionItemLabel
            no label
            link is RSSLPLCompanionItems
            display as tag
                color of brown
            visible when (HasCompanionItem)
        DerivedItemAvailableSOH
            label is "StockOnHand"
            link is ItemLocationBalancesNav
            visible when (DisplayStockOnHand)
        DefaultUnitCost
            visible when (false)
        button row
            button of "<RecipeItemLabel>" with icon.spoon-and-fork
                visible when (IsRecipeItem)
            button of "<DerivedVendorAndPurchaseFrom>" with icon.truck
                visible when (DerivedVendor entered)
            button of "<DerivedManufacturerInformation>" with icon.manufacturing
                visible when (DerivedManufacturerInformation entered)
            button of "AddToFavoriteList" with icon.star-outlined
                visible when (DisplayOutlinedFavoritesIcon)
                action is RSSLPLMarkAsFavorite
            button of "FavoriteItem" with icon.star-yellow
                visible when (DisplayYellowFavoritesIcon)
                action is RSSLPLMarkAsFavorite
    right column
        DefaultItemType
            no label
            right align
            display as tag
                color of green when (DefaultItemType.Inventoried)
                color of gray when (DefaultItemType.Service)
                color of blue when (DefaultItemType.Special)
                color of purple when (DefaultItemType.NonStock)
        DefaultUOMConversion
            no label
        button of "AddToRequisition" with icon.cart
            visible when (not RSSLPLCurrentRequisitionForRQCEntered)
            sub form is RSSLPLAddNewRequisition
                action is RSSLPLAddSelectedToRequisition
            bold
            is primary
        button of "AddToRequisition" with icon.cart
            visible when (RSSLPLCurrentRequisitionForRQCEntered)
            action is RSSLPLAddSelectedToRequisition
                suppress form open
            bold
            is primary
