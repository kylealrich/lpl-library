RSSLPLCardView is a CardView
    show labels
    left column is "<DerivedImageURL>"
        visible when (UseLinkedImage)
        display as full image
    left column is DerivedPicture
        visible when (not UseLinkedImage)
        display as full image
        missing image is random background
            foreground text is "<RequisitionLine.RequisitionLineItemAbbreviation>"
    main column
        text of "<Item>_-_<Description>"
        ItemDescription2
            no label
        button row
            button of "Open" with icon.open-folder
                link is RSSLPLRequisitionLineDetailNav
            button of "Distributions<DistributionCount>" with icon.ledger
                link is Distributions
                raise red alert when (MissingDistribution)
                    mouse over text is "MissingDistributionAccount"
            button of "Comments<CommentCount>" with icon.comment
                link is RSSLPLComments
            button of "AOC<AddOnChargeCount>" with icon.add-grid-row
                link is RSSLPLAddOnChargesWithCompany
            button of "AddItem_toRequisition" with icon.add-small
                visible when (not IsUnreleased)
                action is ModalCopyRequisitionLine
    right column
        DerivedStatusMessage
            no label
            display as tag
                color of blue when (IsUnreleased)
                color of green when (Status.Processed)
                color of red when (IsRejectedStatus)
                color of yellow when (IsReleasedStatus)
        OnContractLabel
            no label
            link is ContractLineNav
            visible when (HasContractAndDisplayContract)
        SubstituteItemLabel
            no label
            link is OriginalItemNav
            display as tag
                color of yellow
            visible when (OriginalItem entered)
        VendorBackorderMessage
            no label
            display as tag
                color of blue
            visible when (ItemIsBackordered)
        RecallLabel
            no label
            link is RecallNav
            display as tag
                color of red
            visible when (RecallProductItemForItemRelExists)
        LatexLabel
            no label
            display as tag
                color of orange
            visible when (LatexItem)
        MissingDistribMessage
            no label
            display as tag
                color of orange
            visible when (MissingDistribution)
        ErrorsLabel
            no label
            link is RequisitionLineErrors
            display as tag
                color of red
            visible when (LineErrorsExist)
