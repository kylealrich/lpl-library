RSSLPLImageAndActionsCardViewForSpecialAndService is a CardView
    show labels
    left column is "<DerivedImageURL>"
        visible when (UseLinkedImage)
        display as full image
    left column is DerivedPicture
        visible when (not UseLinkedImage)
        display as full image
        missing image is random background
            foreground text is "<RequisitionLine.RequisitionLineItemAbbreviation>"
    main column
        DerivedItemAndDescription
            no label
            bold
        DerivedVendorItemDisplay
            link is VendorItemNav
            visible when (VendorItemExists)
        VendorItem
            label is "VendorItem"
            visible when (HasVendorItemAndDoesNotExist)
        DerivedQuantityUnitCostAvailableQuantity
            no label
            link is ItemLocationBalancesNav
            visible when (ItemType.Inventoried)
        RSSLPLDerivedQuantityAndUnitCostCurrency
            no label
            visible when (not ItemType.Inventoried)
        OnContractLabel
            no label
            link is ContractLineNav
            visible when (HasContractAndDisplayContract)
        SubstituteItemLabel
            no label
            link is OriginalItemNav
            display as tag
                color of yellow
            visible when (OriginalItem entered)
        RecallLabel
            no label
            link is RecallNav
            display as tag
                color of red
            visible when (RecallProductItemForItemRelExists)
        LatexLabel
            no label
            display as tag
                color of orange
            visible when (LatexItem)
        SpecialHandlingCodeLabel
            no label
            display as tag
                color of yellow
            visible when (HasSpecialHandlingCode)
        button row
            button of "<RecipeItemLabel>" with icon.spoon-and-fork
                visible when (Item.Recipe)
            button of "<DerivedVendorAndPurchaseFrom>" with icon.truck
                visible when (Vendor entered)
            button of "<DerivedManufacturerInformation>" with icon.manufacturing
                visible when (DerivedManufacturerInformation entered)
    right column
        ItemType
            no label
            display as tag
                color of green when (ItemType.Inventoried)
                color of gray when (ItemType.Service)
                color of blue when (ItemType.Special)
                color of purple when (ItemType.NonStock)
        ItemUOMConversion
            no label
        button of "AddToRequisition" with icon.cart
            visible when (actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC not entered)
            action is ModalCopyRequisitionLineForSpecialAndService
            bold
            is primary
        button of "AddToRequisition" with icon.cart
            visible when (actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC entered)
            action is RSSLPLModalCopyLineOptionPromptForSpecialAndService
            bold
            is primary
