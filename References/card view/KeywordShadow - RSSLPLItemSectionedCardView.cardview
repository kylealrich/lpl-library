RSSLPLItemSectionedCardView is a SectionedCardView
    Image Section
        image is DerivedPicture
            use 12 lines
            tag is DefaultItemType
                visible when (DefaultItemType entered)
                color of green when (DefaultItemType.Inventoried)
                color of gray when (DefaultItemType.Service)
                color of blue when (DefaultItemType.Special)
                color of purple when (DefaultItemType.NonStock)
    Title Section
        title is "<DerivedItem>"
            link is RSSLPLCatalogItemDetailView
    Fields Section
        ItemDescription
            no label
        DefaultUnitCost
            label is "UnitCost"
            bold
            raise red alert when (MissingDefaultUnitCost)
                mouse over text is "DefaultUnitCostNotEntered"
        text of "UOM:<DerivedCostUOM>_:_<DefaultUOMConversion>"
        DerivedItemAvailableSOH
            label is "StockOnHand"
            bold
            link is ItemLocationBalancesNav
            visible when (DisplayStockOnHand)
    Row Section
        LatexLabel
            no label
            display as tag
                color of orange
            visible when (LatexItem)
        RecallLabel
            no label
            link is RecallNav
            display as tag
                color of red
            visible when (RecallNoticesExist)
        SpecialHandlingCodeLabel
            no label
            display as tag
                color of yellow
            visible when (HasSpecialHandlingCode)
    Action Section
        button of "<RecipeItemLabel>" with icon.spoon-and-fork
            visible when (IsRecipeItem)
        button of "<ReplacementItemLabel>" with icon.info-alert
            visible when (IsReplacementItem)
        button of "<SubstituteItemLabel>" with icon.info
            visible when (HasSubstituteItem)
            link is RSSLPLSubstituteItems
            display as link
        button of "<CompanionItemLabel>" with icon.info-linear
            visible when (HasCompanionItem)
            link is RSSLPLCompanionItems
            display as link
        button of "Contract:<DerivedContractID>ContractLine:<DerivedContractLine>" with icon.star-filled
            visible when (HasContractLineAndDisplayContract)
            link is ContractLineNav
            display as link
        button of "<DerivedVendorAndPurchaseFrom>" with icon.truck
            visible when (DerivedVendor entered)
        button of "<DerivedManufacturerInformation>" with icon.manufacturing
            visible when (DerivedManufacturerCode entered)
        button of "<KitItemLabel>" with icon.utilities
            visible when (IsKitItem)
            link is RSSLPLKitItemComponents
            display as link
        button of "Add"
            visible when (not RSSLPLCurrentRequisitionForRQCEntered)
            sub form is RSSLPLAddNewRequisition
                action is RSSLPLAddSelectedToRequisition
            label is "_"
            is primary
        button of "Add"
            visible when (RSSLPLCurrentRequisitionForRQCEntered)
            action is RSSLPLAddSelectedToRequisition
                suppress form open
            label is "_"
            is primary
