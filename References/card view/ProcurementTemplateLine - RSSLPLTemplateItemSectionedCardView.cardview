RSSLPLTemplateItemSectionedCardView is a SectionedCardView
    Image Section
        image is DerivedPicture
            use 12 lines
            tag is DefaultItemType
                visible when (DefaultItemType entered)
                color of green when (DefaultItemType.Inventoried)
                color of gray when (DefaultItemType.Service)
                color of blue when (DefaultItemType.Special)
                color of purple when (DefaultItemType.NonStock)
    Title Section
        title is "<Item>"
            link is TemplateLineDetailView
    Fields Section
        ItemDescription
            no label
        DisplayOrder
            label is "Line"
        DefaultUnitCost
            label is "UnitCost"
        text of "UOM:<DerivedUOM>_:_<DefaultUOMConversion>"
        DerivedItemAvailableSOH
            label is "StockOnHand"
            link is ItemLocationBalancesNav
            visible when (DisplayStockOnHand)
    Row Section
        LatexLabel
            no label
            display as tag
                color of orange
            visible when (LatexItem)
        RecallLabel
            no label
            link is RecallNav
            display as tag
                color of red
            visible when (RecallNoticesExist)
        SpecialHandlingCodeLabel
            no label
            display as tag
                color of yellow
            visible when (HasSpecialHandlingCode)
    Action Section
        button of "<RecipeItemLabel>" with icon.spoon-and-fork
            visible when (Item.Recipe)
        button of "OnContract<DerivedContractID>" with icon.star-filled
            visible when (HasContractLineAndDisplayContract)
            link is ContractLineNav
            display as link
        button of "<DerivedVendorAndPurchaseFrom>" with icon.truck
            visible when (DerivedVendor entered)
        button of "<SubstituteItemLabel>" with icon.info
            visible when (HasSubstituteItem)
            link is RSSLPLSubstituteItems
            display as link
        button of "<DerivedManufacturerInformation>" with icon.manufacturing
            visible when (DerivedManufacturerCode entered)
        button of "<KitLabel>" with icon.utilities
            visible when (IsKitItem)
            link is RSSLPLKitItemComponents
            display as link
        button of "Add"
            visible when (actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC not entered)
            sub form is RSSLPLAddNewRequisition
                action is RSSLPLAddSelectedToRequisition
            label is "_"
            is primary
        button of "Add"
            visible when (actor.agent(Employee).Employee.Requester.CurrentRequisitionForRQC entered)
            action is RSSLPLAddSelectedToRequisition
                suppress form open
            label is "_"
            is primary
